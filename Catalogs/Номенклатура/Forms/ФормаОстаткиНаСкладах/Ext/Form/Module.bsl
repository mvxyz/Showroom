
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТипЦены = Параметры.ТипЦены;
	Номенклатура = Параметры.Номенклатура;	
	Характеристика = Параметры.ХарактеристикаНоменклатуры;	
	
	Элементы.СерияНоменклатуры.Видимость = Параметры.ОтображатьСерии И ЕстьЗаполненныеСерии();
	
	Список.Параметры.УстановитьЗначениеПараметра("Номенклатура", Номенклатура);
	Список.Параметры.УстановитьЗначениеПараметра("ХарактеристикаНоменклатуры", Характеристика);
	
	СвободныйОстатокВсегоЧисло = ОбщиеПроцедурыВызовСервера.ПолучитьСвободныйОстатокТовара(Номенклатура, Характеристика);
	СвободныйОстатокВсего = ?(СвободныйОстатокВсегоЧисло = 0, "Ноль", СокрЛП(СвободныйОстатокВсегоЧисло));
	
	ДанныеЦены = ОбщиеПроцедурыВызовСервера.ПолучитьЦенуНоменклатуры(Номенклатура, ТипЦены, Характеристика);
	ЗаполнитьЗначенияСвойств(ЭтаФорма, ДанныеЦены);
	
	ЕстьОстаток = СвободныйОстатокВсегоЧисло <> 0; 
	
	Элементы.ЕдиницаИзмерения.Видимость = ЕстьОстаток;
	
	Элементы.Список.Видимость = ЕстьОстаток;
	Элементы.ДекорацияНетСвободногоОстатка.Видимость = НЕ ЕстьОстаток; 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВКорзину(Команда)
	
	Корзина = ОбщиеПроцедурыВызовСервера.ПолучитьКорзину(); 	
	Если Корзина = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ФормаКорзина = ОбщегоНазначенияКлиент.ПолучитьФормуКорзина();
	
	Если ФормаКорзина = Неопределено  Тогда
	   Возврат;
	КонецЕсли;
	
	Если ФормаКорзина.Открыта() Тогда
	    ФормаКорзина.Закрыть();
	КонецЕсли;
	
	ПараметрыТовара = ПолучитьПараметрыТовара();
	
	ОбщиеПроцедурыВызовСервера.ДобавитьТоварВКоризну(ПараметрыТовара);

КонецПроцедуры

&НаКлиенте
Процедура ВЗаказ(Команда)
	
	ПараметрыФормы = Новый Структура("ПараметрыТовара", ПолучитьПараметрыТовара());
	ОткрытьФорму("Документ.Заказ.ФормаВыбора", ПараметрыФормы, ЭтаФорма);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЕстьЗаполненныеСерии()
	
	ЕстьЗаполненныеСерии = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РегистрСведенийТоварыОстатки.СерияНоменклатуры КАК СерияНоменклатуры
	|ИЗ
	|	РегистрСведений.ТоварыОстатки КАК РегистрСведенийТоварыОстатки
	|ГДЕ
	|	РегистрСведенийТоварыОстатки.Номенклатура = &Номенклатура
	|	И РегистрСведенийТоварыОстатки.ХарактеристикаНоменклатуры = &ХарактеристикаНоменклатуры";
	
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры", Характеристика);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		Если ЗначениеЗаполнено(Выборка.СерияНоменклатуры) Тогда
			ЕстьЗаполненныеСерии = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ЕстьЗаполненныеСерии;
	
КонецФункции

&НаКлиенте
Функция ПолучитьПараметрыТовара()
	
	ПараметрыТовара = Новый Структура;
	ПараметрыТовара.Вставить("Номенклатура", Номенклатура);
	ПараметрыТовара.Вставить("ТипЦены", ТипЦены);
	ПараметрыТовара.Вставить("Цена", Цена);
	ПараметрыТовара.Вставить("ЕдиницаИзмерения", ЕдиницаИзмерения);
	ПараметрыТовара.Вставить("ХарактеристикаНоменклатуры", Характеристика);
		
	Возврат ПараметрыТовара;

КонецФункции 

#КонецОбласти




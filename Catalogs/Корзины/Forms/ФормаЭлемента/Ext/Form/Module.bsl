
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ПараметрыТовара") Тогда
		
		НоваяСтрока = Объект.СоставКорзины.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Параметры.ПараметрыТовара);
		НоваяСтрока.ЕдиницаИзмерения = НоваяСтрока.Номенклатура.ЕдиницаИзмерения;
		Записать();
		
	КонецЕсли;
	
	Элементы.СоставКорзиныХарактеристикаНоменклатуры.Видимость = 
		ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристикиНоменклатуры");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ВыполненаПопыткаПереносаТоваровИзКорзиныВЗаказ" Тогда
		
		Если ОчищатьПослеПереносаВЗаказ Тогда
			
			Объект.СоставКорзины.Очистить();
			
			Для каждого Элемент Из Параметр Цикл
				
				Если Элемент.Пометка Тогда
					НоваяСтрока = Объект.СоставКорзины.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, Элемент.Значение); 
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
		Записать();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСоставКорзины

&НаКлиенте
Процедура СоставКорзиныПриИзменении(Элемент)
	
	Записать();
	
КонецПроцедуры

&НаКлиенте
Процедура СоставКорзиныНоменклатураПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.СоставКорзины.ТекущиеДанные;
	
	ТекущиеДанные.Количество = 1;
	
	ТекущиеДанные.ЕдиницаИзмерения = ПолучитьЕдиницуИзмерения(ТекущиеДанные.Номенклатура);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Очистить(Команда)
	
	ПоказатьВопрос(Новый ОписаниеОповещения("ОчиститьЗавершение", ЭтотОбъект), "Очистить?", РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Нет);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Объект.СоставКорзины.Очистить();
		Записать();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиВЗаказ(Команда)
	
	Если Объект.СоставКорзины.Количество() = 0 Тогда
		ТекстСообщения = "Нет товаров для переноса.";
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения); 
		Возврат;
	КонецЕсли;
	
	ТоварыКорзины = ПолучитьТоварыКорзины();
	
	Если ТоварыКорзины = Неопределено Тогда
	  Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТоварыКорзины", ТоварыКорзины);
	
	ОткрытьФорму("Документ.Заказ.ФормаВыбора", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает список значений со структурами данных товаров корзины
//
&НаКлиенте
Функция ПолучитьТоварыКорзины()

	ТоварыКорзины = Новый СписокЗначений;
	
	ЕстьНезаполненныеСтроки = Ложь;
	
	Для каждого Стр Из Объект.СоставКорзины Цикл
		
		Если НЕ ЗначениеЗаполнено(Стр.Номенклатура) Тогда
			ТекстСообщения = "В строке " + Стр.НомерСтроки + " не выбран товар!";
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,,,ЕстьНезаполненныеСтроки); 
		КонецЕсли;
		
		Если Стр.Количество = 0 Тогда
			ТекстСообщения = "В строке " + Стр.НомерСтроки + " не введено количество!";
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,,,ЕстьНезаполненныеСтроки); 
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Стр.ЕдиницаИзмерения) Тогда
			ТекстСообщения = "В строке " + Стр.НомерСтроки + " не заполнена единица измерения!";
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,,,ЕстьНезаполненныеСтроки); 
		КонецЕсли;
		
		Если НЕ ЕстьНезаполненныеСтроки Тогда
			Товар = Новый Структура("Номенклатура, ХарактеристикаНоменклатуры, Количество, ЕдиницаИзмерения") ;
			ЗаполнитьЗначенияСвойств(Товар, Стр);
			ТоварыКорзины.Добавить(Товар);
		КонецЕсли;
		 
	КонецЦикла;
	
	Если ЕстьНезаполненныеСтроки Тогда
		Возврат Неопределено;
	Иначе
		Возврат ТоварыКорзины;
	КонецЕсли;

КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьЕдиницуИзмерения(Номенклатура)
	
	Возврат Номенклатура.ЕдиницаИзмерения;
	
КонецФункции

&НаКлиенте
Процедура ПеренестиВНовыйЗаказ(Команда)
	
	Если Объект.СоставКорзины.Количество() = 0 Тогда
		ТекстСообщения = "Нет товаров для переноса.";
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения); 
		Возврат;
	КонецЕсли;
	
	ТоварыКорзины = ПолучитьТоварыКорзины();
	
	Если ТоварыКорзины = Неопределено Тогда
	  Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТоварыКорзины", ТоварыКорзины);
	
	ОткрытьФорму("Документ.Заказ.ФормаОбъекта", ПараметрыФормы);

КонецПроцедуры

#КонецОбласти



#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Запись.ИдентификаторОбъекта = УникальныйИдентификаторПустой() Тогда

		Элементы.ТипОбъекта.Видимость           = Ложь;
		Элементы.ИдентификаторОбъекта.Видимость = Ложь;

	КонецЕсли;

	СписокТекстом = 
		"<html>
		|<head>
		|	<meta name=""viewport"" %content% http-equiv=""Content-Type""/>
		|</head>
		|<script type=""text/javascript"">
		|function ShowHideContent(spoiler, clicked) 
		|{
		|	if (spoiler.style.display != 'none')
		|	{
		|		spoiler.style.display = 'none';
		|		clicked.innerHTML = clicked.innerHTML.replace(""--"",""+"");
		|		clicked.innerHTML = clicked.innerHTML.replace(""Скрыть"",""Показать"");
		|	}
		|else
		|	{
		|		spoiler.style.display = 'block';
		|		clicked.innerHTML = clicked.innerHTML.replace(""+"",""--"");
		|		clicked.innerHTML = clicked.innerHTML.replace(""Показать"",""Скрыть"")
		|	}
		|}
		|</script>
		|<body>
		|	<div style = ""font-family:Arial;font-size:%1px;line-height: 1.1"">";

	Если ЭтоУстройствоСБольшимЭкраном() Тогда
		СписокТекстом = СтрЗаменить(СписокТекстом, "%1", "14");
	Иначе
		СписокТекстом = СтрЗаменить(СписокТекстом, "%1", "12");
	КонецЕсли;

	Счетчик       = 1;
	ЕстьОшибки    = Ложь;
	СписокТекстом = СтрЗаменить(
		СписокТекстом, 
		"%content%", 
		"content = ""text/html,width=320,initial-scale=1, user-scalable = no""");

	Если Запись.ТипСобытия = Перечисления.ТипыСобытийПротокола.Информация Тогда
		Картинка = БиблиотекаКартинок.ЗадачаУспешноеВыполнение;

	ИначеЕсли Запись.ТипСобытия = Перечисления.ТипыСобытийПротокола.Предупреждение Тогда
		Картинка = БиблиотекаКартинок.ЗадачаНеУспешноеВыполнение;

	ИначеЕсли Запись.ТипСобытия = Перечисления.ТипыСобытийПротокола.Ошибка Тогда
		Картинка   = БиблиотекаКартинок.КомандаУдалитьСтроку;
		ЕстьОшибки = Истина;

	КонецЕсли;

	Если ТипЗнч(Картинка) = Тип("Картинка") Тогда
		ДвоичныеДанныеФото = Картинка.ПолучитьДвоичныеДанные();
		Формат = Строка(Картинка.Формат());
	Иначе
		ДвоичныеДанныеФото = Неопределено;
		Формат = ФорматКартинки.НеизвестныйФормат;
	КонецЕсли;

	СтрокаИсточника = "data:image/" + Формат + ";base64," + Base64Строка(ДвоичныеДанныеФото);

	ПозицияНачалаОписанияОшибки = СтрНайти(Запись.Описание, ":#");

	Если ПозицияНачалаОписанияОшибки > 0 Тогда

		СтрокаНачало = СтрШаблон(
			"<a id=""aid%1"" href=""javascript:void(0)""
				|onclick=""ShowHideContent(document.getElementById('id%1'), this)"">%2</a>",
			Строка(Счетчик),
			НСтр("ru = 'Показать дополнительную информацию (для администратора)'; 
				 |en = 'Show more (for administrators)'"));

		ПозицияНачалаТекстаОбОшибке = ПозицияНачалаОписанияОшибки + 2;
		СтрокаТекстОшибкиДляАдминистратора = СтрШаблон(
			"<div id=""id%1"" style=""display: block; margin:0px 0px 0px 10px;"">%2</div>",
			Строка(Счетчик),
			Сред(Запись.Описание, ПозицияНачалаТекстаОбОшибке));

		СтрокаТекстОшибкиДляАдминистратора = 
			СтрЗаменить(СтрокаТекстОшибкиДляАдминистратора, Символы.ПС, "<br/>");

		СтрокаТекстОшибкиДляПользователя = 
			Сред(Запись.Описание, 1, СтрНайти(Запись.Описание, "#") - 1);

		СтрокаТекстОшибкиДляПользователя = 
			СтрЗаменить(СтрокаТекстОшибкиДляПользователя, Символы.ПС, "<br/>");

		ОднаСтрока = СтрШаблон(
			"<div>%1%2%3</div><hr/>",
			СтрокаТекстОшибкиДляПользователя,
			СтрокаНачало,
			СтрокаТекстОшибкиДляАдминистратора);
	Иначе

		ОднаСтрока = СтрШаблон(
			"<div>%1</div><hr/>",
			СтрЗаменить(Запись.Описание, Символы.ПС, "<br/>"));

	КонецЕсли;

	Если ЭтоУстройствоСБольшимЭкраном() Тогда
		ОднаСтрока = СтрЗаменить(ОднаСтрока, "#height", "24");
	Иначе
		ОднаСтрока = СтрЗаменить(ОднаСтрока, "#height", "16");
	КонецЕсли;

	СписокТекстом = СписокТекстом + ОднаСтрока;

	СписокТекстом = СписокТекстом + "</div></body></html>";

КонецПроцедуры

&НаКлиенте
Процедура ОписаниеПриИзменении(Элемент)

	Модифицированность = Ложь;

КонецПроцедуры

#КонецОбласти


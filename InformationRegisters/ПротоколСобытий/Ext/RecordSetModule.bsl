#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	// Заполнение индекса типа события для отображения картинки в списке
	Для Каждого Запись Из ЭтотОбъект Цикл

		Запись.ТипСобытияИндекс = 0;

		Если Запись.ТипСобытия = Перечисления.ТипыСобытийПротокола.Предупреждение Тогда
			Запись.ТипСобытияИндекс = 1;

		ИначеЕсли Запись.ТипСобытия = Перечисления.ТипыСобытийПротокола.Ошибка Тогда
			Запись.ТипСобытияИндекс = 2;

		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Если ЭтотОбъект.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	// При записи каждой 10-й записи удаляются записи протокола старше 7 дней.
	ГенераторЧисел = Новый ГенераторСлучайныхЧисел();
	ОчередноеЧисло = ГенераторЧисел.СлучайноеЧисло(1, 10);
	Если ОчередноеЧисло = 5 Тогда

		ГраницаУдаления = ТекущаяДата() - 7 * 24 * 60 * 60;
		ПротоколВыборка = РегистрыСведений.ПротоколСобытий.Выбрать(, ГраницаУдаления);

		Пока ПротоколВыборка.Следующий() Цикл

			МенеджерЗаписи = РегистрыСведений.ПротоколСобытий.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Период        = ПротоколВыборка.Период;
			МенеджерЗаписи.МоментВремени = ПротоколВыборка.МоментВремени;

			МенеджерЗаписи.Удалить();

		КонецЦикла;

	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецЕсли

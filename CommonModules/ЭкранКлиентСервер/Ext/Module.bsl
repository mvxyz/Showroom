
#Область ПрограммныйИнтерфейс

// Проверяет, что устройство имеет основной экран больше 7 дюймов.
//
// Возвращаемое значение:
//  Истина - Если значение константы диагональ больше 7.
//
Функция УстройствоИмеетЭкранБольшеСемиДюймов() Экспорт

	Возврат ДиагональЭкрана() > 7;

КонецФункции

// Возвращает диагональ основного экрана устройства.
// 
// Возвращаемое значение:
//  Число - величина диагонали 1-го экрана мобильного устройства.
//
Функция ДиагональЭкрана() Экспорт

	ПараметрыЭкрана = ПараметрыЭкрана();
	Если ПараметрыЭкрана.Количество() = 0 Тогда
		Диагональ = ОбщегоНазначенияВызовСервера.ПолучитьЗначениеКонстанты("ДиагональЭкрана");
	Иначе
		Диагональ = ПараметрыЭкрана[0].Диагональ;
	КонецЕсли;

	Возврат Диагональ;

КонецФункции 

// Возвращает массив с структурами параметров каждого экрана устройства.
// 
// Возвращаемое значение:
//  Массив - Массив параметров всех экранов мобильного устройства.
//
Функция ПараметрыЭкрана() Экспорт

	Инфо = ПолучитьИнформациюЭкрановКлиента();

	ДанныеОбЭкранах = Новый Массив();

	Если Не Инфо = Неопределено Тогда

		Для Каждого ДанныеОбЭкране Из Инфо Цикл

			ПараметрыЭкрана = Новый Структура();
			ПараметрыЭкрана.Вставить("Ширина"    , ДанныеОбЭкране.Ширина);
			ПараметрыЭкрана.Вставить("Высота"    , ДанныеОбЭкране.Высота);
			ПараметрыЭкрана.Вставить("Разрешение", ДанныеОбЭкране.DPI);
			ПараметрыЭкрана.Вставить("СоотношениеСторон"   , ДанныеОбЭкране.Высота / ДанныеОбЭкране.Ширина);
			ПараметрыЭкрана.Вставить("ПортретнаяОриентация", ДанныеОбЭкране.Высота > ДанныеОбЭкране.Ширина);
			ПараметрыЭкрана.Вставить("Диагональ", 
				Окр(Sqrt(Pow(ДанныеОбЭкране.Ширина,2) + Pow(ДанныеОбЭкране.Высота,2)) / ДанныеОбЭкране.DPI, 0));

			ДанныеОбЭкранах.Добавить(ПараметрыЭкрана);

		КонецЦикла;

	КонецЕсли;

	Возврат ДанныеОбЭкранах;

КонецФункции

#КонецОбласти

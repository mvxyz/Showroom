
#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если НЕ ЗначениеЗаполнено(Ответственный)
		ИЛИ Ответственный.ИдентификаторПользователяИБ = УникальныйИдентификаторПустой() Тогда
		
		Ответственный = Пользователи.ТекущийПользователь();
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ВалютаДокумента) Тогда
		
		ТекстСообщения = "Не заполнена валюта документа";
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,,Отказ); 
		
	КонецЕсли;
	
	СуммаДокумента = Товары.Итог("Сумма");
	
КонецПроцедуры

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Префикс = ПолучитьФункциональнуюОпцию("ПрефиксМобильногоКлиента");
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Если Товары.Количество() = 0 Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	КонтролироватьОстатки = ДополнительныеСвойства.Свойство("КонтролироватьОстатки") 
							И ДополнительныеСвойства.КонтролироватьОстатки;
		
	Для каждого Стр Из Товары Цикл
		
		Если КонтролироватьОстатки Тогда 
			
			СвободныйОстаток = ОбщиеПроцедурыВызовСервера.ПолучитьСвободныйОстатокТовара(Стр.Номенклатура, Стр.ХарактеристикаНоменклатуры);
			Если СвободныйОстаток < Стр.Количество Тогда
				ТестСообщения = "В строке " + Стр.НомерСтроки + " количество " + Стр.Количество + 
				" превышает свободный остаток " + СвободныйОстаток + ". ";
				ОбщегоНазначения.СообщитьПользователю(ТестСообщения,,,,Отказ);
			КонецЕсли;
			
		КонецЕсли;
		
		Если Стр.Количество = 0 Тогда
			ТестСообщения = "В строке " + Стр.НомерСтроки + " количество равно нулю. Нужно ввести количество!";
			ОбщегоНазначения.СообщитьПользователю(ТестСообщения,,,,Отказ);
		КонецЕсли;
		
		Если Стр.Цена = 0 Тогда
			ТестСообщения = "В строке " + Стр.НомерСтроки + " цена равна нулю. Нужно ввести цену!";
			ОбщегоНазначения.СообщитьПользователю(ТестСообщения,,,,Отказ);
		КонецЕсли;
		
	КонецЦикла;
	
	Если Отказ Тогда
		ОбщегоНазначения.СообщитьПользователю("Документ не проведен!",,,,Отказ);
	КонецЕсли; 

КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
		Ответственный = Пользователи.ТекущийПользователь();
		
КонецПроцедуры

#КонецОбласти

